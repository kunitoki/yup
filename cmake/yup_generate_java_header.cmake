# ==============================================================================
#
#   This file is part of the YUP library.
#   Copyright (c) 2025 - kunitoki@gmail.com
#
#   YUP is an open source library subject to open-source licensing.
#
#   The code included in this file is provided under the terms of the ISC license
#   http://www.isc.org/downloads/software-support-policy/isc-license. Permission
#   To use, copy, modify, and/or distribute this software for any purpose with or
#   without fee is hereby granted provided that the above copyright notice and
#   this permission notice appear in all copies.
#
#   YUP IS PROVIDED "AS IS" WITHOUT ANY WARRANTY, AND ALL WARRANTIES, WHETHER
#   EXPRESSED OR IMPLIED, INCLUDING MERCHANTABILITY AND FITNESS FOR PURPOSE, ARE
#   DISCLAIMED.
#
# ==============================================================================

# Script to generate C++ header with embedded Java bytecode

if (NOT INPUT_FILE)
    message (FATAL_ERROR "INPUT_FILE must be specified")
endif()

if (NOT OUTPUT_FILE)
    message (FATAL_ERROR "OUTPUT_FILE must be specified")
endif()

if (NOT VARIABLE_NAME)
    set (VARIABLE_NAME "javaBytecode")
endif()

# Read the compressed DEX file as binary
file (READ "${INPUT_FILE}" hex_content HEX)
get_filename_component (INPUT_FILE_NAME "${INPUT_FILE}" NAME)

# Convert hex string to C++ array format
set (count 0)
set (wrapped_hex "")
string (REGEX MATCHALL "([A-Fa-f0-9][A-Fa-f0-9])" separated_hex "${hex_content}")
foreach (byte IN LISTS separated_hex)
    if (count EQUAL 0)
        set (current_line "0x${byte}")
    else()
        set (current_line "${current_line},0x${byte}")
    endif()
    math (EXPR count "${count} + 1")
    if (count EQUAL 60)
        list (APPEND wrapped_hex "${current_line}")
        set (count 0)
    endif()
endforeach()
if (count GREATER 0)
    list(APPEND wrapped_hex "${current_line}")
endif()
string (JOIN "\n" formatted_hex ${wrapped_hex})

# Generate timestamp
string (TIMESTAMP current_time "%Y-%m-%d %H:%M:%S UTC" UTC)

# Get file size
file (SIZE "${INPUT_FILE}" file_size)

# Generate the header content
set (header_content "/*
  ==============================================================================

   This file is part of the YUP library.
   Copyright (c) 2025 - kunitoki@gmail.com

   YUP is an open source library subject to open-source licensing.

   The code included in this file is provided under the terms of the ISC license
   http://www.isc.org/downloads/software-support-policy/isc-license. Permission
   to use, copy, modify, and/or distribute this software for any purpose with or
   without fee is hereby granted provided that the above copyright notice and
   this permission notice appear in all copies.

   YUP IS PROVIDED \"AS IS\" WITHOUT ANY WARRANTY, AND ALL WARRANTIES, WHETHER
   EXPRESSED OR IMPLIED, INCLUDING MERCHANTABILITY AND FITNESS FOR PURPOSE, ARE
   DISCLAIMED.

  ==============================================================================
*/

#pragma once

//==============================================================================
// This file is auto-generated by the YUP build system.
// DO NOT EDIT THIS FILE MANUALLY!
//
// Generated: ${current_time}
//==============================================================================

// clang-format off
constexpr unsigned char ${VARIABLE_NAME}[]
{
    ${formatted_hex}
};
// clang-format on

constexpr auto ${VARIABLE_NAME}Size = sizeof(${VARIABLE_NAME});
")

# Write the header file
file (WRITE "${OUTPUT_FILE}" "${header_content}")

message (STATUS "Generated Java bytecode header: ${OUTPUT_FILE} (${file_size} bytes)")
